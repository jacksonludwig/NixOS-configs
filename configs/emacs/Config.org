#+title: Init.el Generator
#+property: header-args:emacs-lisp :tangle ~/.emacs.d/init.el
#+startup: fold

* Note on use
I tend to use babel to easily enable/disable sections of these config. I add ~:tangle no~ to code blocks to do so. This is not clear on github's view, though.
* Core settings and variables
Basic settings
#+begin_src emacs-lisp
(setq user-full-name "Jackson Ludwig"
      user-mail-address "jacksonludwig0@gmail.com")

(setq backup-directory-alist
      `((".*" . ,temporary-file-directory)))
(setq auto-save-file-name-transforms
      `((".*" ,temporary-file-directory t)))

;; SAVE CUSTOM VARS TO SEPARATE FILE
(setq custom-file (concat user-emacs-directory "custom.el"))
(load custom-file 'noerror)
#+end_src
* Straight/Use-package combined setup
#+begin_src emacs-lisp
;; Bootstrap straight
;; -- Variables
(setq straight-use-package-by-default t)
(setq package-native-compile t) ;; not needed or just straight

;; -- Bootstrap `straight.el'
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
      (bootstrap-version 5))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

(straight-use-package 'use-package)
#+end_src
* Basic Setup
** UI
#+begin_src emacs-lisp
(scroll-bar-mode -1)        ; Disable visible scrollbar
(tool-bar-mode -1)          ; Disable the toolbar
(tooltip-mode -1)           ; Disable tooltips

(menu-bar-mode -1)          ; Disable the menu bar
(setq visible-bell t)       ; Visual bell

(setq initial-scratch-message ";; Emacs\n\n") ;; scratch text
(setq inhibit-startup-screen t) ;; Just show scratch
#+end_src
** Built-in setting changes
#+begin_src emacs-lisp
(column-number-mode) ;; show line nums in modeline

(setq display-line-numbers-type 'relative)
;;(global-display-line-numbers-mode t) ; line numbers (maybe not needed without evil?)

(setq-default truncate-lines t) ; Don't wrap by default

(setq help-window-select t) ; Auto switch to help buffers (usually just reading and quitting right away)

(defalias 'yes-or-no-p 'y-or-n-p) ; y or n is enough

;;(setq whitespace-style '(face trailing tabs tab-mark)) ; show whitespace

(show-paren-mode t) ;; show matching parens

(add-hook 'prog-mode-hook 'electric-pair-local-mode) ; electric pair only in prog modes
#+end_src
* Font setup
#+begin_src emacs-lisp
(defvar jackson/default-font-size 125)
(defvar jackson/main-font nil)
(defvar jackson/variable-font nil)

(set-face-attribute 'default nil :family jackson/main-font :height jackson/default-font-size)

;; Set the fixed pitch face
(set-face-attribute 'fixed-pitch nil :family jackson/main-font :height 1.0)

;; Set the variable pitch face
(set-face-attribute 'variable-pitch nil :family jackson/variable-font :height 1.0)
#+end_src

These functions scale the emacs's global font sizes instead of just buffer.
#+begin_src emacs-lisp
(defvar original-font-size jackson/default-font-size)

(defun adjust-font-size (delta)
  (let* ((old-size (face-attribute 'default :height))
         (new-size (max (max delta (- delta)) (min 200 (+ delta old-size)))))
    (setq original-font-size (or original-font-size old-size))
    (set-face-attribute 'default nil :height new-size)
    (message "Font size set to %d (was %d)" (face-attribute 'default :height) old-size)))

(defun zoom-in ()
  (interactive)
  (adjust-font-size +10))

(defun zoom-out ()
  (interactive)
  (adjust-font-size -10))

(defun zoom-reset ()
  (interactive)
  (when original-font-size
    (set-face-attribute 'default nil :height original-font-size)))

(global-set-key (kbd "C-M-=") 'zoom-in)
(global-set-key (kbd "C-M--") 'zoom-out)
(global-set-key (kbd "C-M-0") 'zoom-reset)
#+end_src
* Core packages and keybinds
** Diminish
#+begin_src emacs-lisp
(use-package diminish
  :config
  (diminish 'global-whitespace-mode)
  (diminish 'visual-line-mode))
#+end_src
** Evil
#+begin_src emacs-lisp
(use-package evil
  :init
  (setq evil-undo-system 'undo-fu)
  (setq evil-want-integration t)
  (setq evil-want-keybinding nil)
  (setq evil-want-C-u-scroll t)
  (setq evil-want-C-i-jump nil)
  (setq evil-mode-line-format nil)
  ;;(setq evil-disable-insert-state-bindings t) ;; use emacs binds instead of insert binds
  :config
  (evil-mode 1)
  (evil-set-initial-state 'messages-buffer-mode 'normal)
  )

(use-package evil-collection
  :after evil
  :custom
  (evil-collection-company-use-tng nil)
  :init
  (evil-collection-init 'vterm))

(use-package evil-org
  :after org
  :diminish evil-org-mode
  :hook (org-mode . evil-org-mode)
  )
#+end_src
** General
#+begin_src emacs-lisp
(use-package general
  :config
  (general-create-definer jackson/leader-binds
    :prefix "C-c")

  (jackson/leader-binds
    "t"   '(:ignore t :which-key "toggles")
    "f"   '(:ignore t :which-key "files")
    "q"   '(:ignore t :which-key "exit menu")
    "qq"  '(kill-emacs :which-key "kill emacs")
    "o"   '(:ignore t :which-key "open")
    )
  )
#+end_src
** Which-key
#+begin_src emacs-lisp
(use-package which-key
  :init (which-key-mode)
  :diminish which-key-mode)
#+end_src
** Counsel/Ivy
#+begin_src emacs-lisp
(use-package prescient)

(use-package counsel
  :after prescient
  :diminish counsel-mode
  :config
  (counsel-mode 1))

(use-package ivy
  :diminish
  :after counsel
  :bind (("C-s" . swiper)
         :map ivy-minibuffer-map
         ("TAB" . ivy-alt-done)
         ("C-l" . ivy-alt-done)
         :map ivy-switch-buffer-map
         ("C-l" . ivy-done)
         ("C-d" . ivy-switch-buffer-kill)
         :map ivy-reverse-i-search-map
         ("C-d" . ivy-reverse-i-search-kill))
  :config
  (ivy-mode 1)

  (jackson/leader-binds
    "fr"  '(counsel-recentf :which-key "recent files")
    "ff"  '(counsel-find-file :which-key "find files")
    "tt"  '(counsel-load-theme :which-key "choose theme")
    ))

(use-package ivy-rich
  :after ivy
  :init
  (ivy-rich-mode 1))

(use-package ivy-prescient
  :after ivy-rich
  :config
  (ivy-prescient-mode 1)
  (prescient-persist-mode 1))
#+end_src
** Vterm
#+begin_src emacs-lisp
(use-package vterm
  :ensure nil
  :hook
  (vterm-mode . (lambda ()
                  (display-line-numbers-mode 0))))

(use-package vterm-toggle
  :config
  (jackson/leader-binds
    "ot"  '(vterm-toggle :which-key "toggle vterm")
    )
  (setq vterm-toggle-fullscreen-p nil)
  (add-to-list 'display-buffer-alist
               '((lambda(bufname _) (with-current-buffer bufname (equal major-mode 'vterm-mode)))
                 (display-buffer-reuse-window display-buffer-at-bottom)
                 (display-buffer-reuse-window display-buffer-in-direction)
                 ;;display-buffer-in-direction/direction/dedicated is added in emacs27
                 (direction . bottom)
                 (dedicated . t) ;dedicated is supported in emacs27
                 (reusable-frames . visible)
                 (window-height . 0.3)))
  )
#+end_src
** Undo-fu
#+begin_src emacs-lisp
(use-package undo-fu
  :config
  (global-unset-key (kbd "C-M-/"))
  (global-set-key (kbd "C-M-/") 'undo-fu-only-redo)
  (global-unset-key (kbd "C-/"))
  (global-set-key (kbd "C-/") 'undo-fu-only-undo)
  )

(use-package undo-fu-session
	:after undo-fu
	:config
	(global-undo-fu-session-mode 1))
#+end_src
** Projectile
#+begin_src emacs-lisp
(use-package projectile
  :diminish projectile-mode
  :custom ((projectile-completion-system 'ivy))
  :config
  (projectile-mode)
  :bind-keymap
  ("C-c p" . projectile-command-map)
  :init
  ;; NOTE: Set this to the folder where you keep your Git repos!
  (when (file-directory-p "~/git_repos/")
    (setq projectile-project-search-path '("~/git_repos/"))))

(use-package counsel-projectile
  :config (counsel-projectile-mode))
#+end_src
* Visual and theme
** All-the-icons
This is required by doom-modeline and occasionally other packages optionally.
#+begin_src emacs-lisp
(use-package all-the-icons)
#+end_src
** Theme/modeline
Modus themes.
#+begin_src emacs-lisp
(use-package modus-themes
  :init
  ;; Load the theme files before enabling a theme
  (modus-themes-load-themes)
  )
#+end_src

Doom themes.
#+begin_src emacs-lisp :tangle no
(use-package doom-themes
  :init
  (load-theme 'doom-tomorrow-night t)
  :config
  (doom-themes-visual-bell-config)
  (doom-themes-org-config)
  (custom-set-faces
   '(org-block-end-line ((t (:background nil)))) ;; avoid bleeding when folded
   '(org-block-begin-line ((t (:background nil)))) ;; symmetry
   '(org-ellipsis ((t (:underline nil))))
   ))
#+end_src
* Language and related support
** Company
#+begin_src emacs-lisp
(use-package company
  :diminish company-mode
  :hook
  (prog-mode . company-mode)
  (org-mode . company-mode)
  (org-mode . (lambda () (setq-local company-idle-delay nil))) ;; manual completion in org buffers
  :bind (:map company-active-map
              ("C-n" . company-select-next)
              ("C-p" . company-select-previous))
  :config
  (setq company-minimum-prefix-length 2
        company-dabbrev-downcase nil
        company-idle-delay 0.4)
  :general
  ("M-n" 'company-complete))
#+end_src
** Flycheck
#+begin_src emacs-lisp
;; Better docs with eglot (if using) and maybe other things
(use-package markdown-mode)

(use-package flycheck
  :hook
  (prog-mode . flycheck-mode)
  :init
  (setq flycheck-check-syntax-automatically '(save mode-enabled))
  )
#+end_src
** Yasnippet
#+begin_src emacs-lisp
  (defun company-yasnippet-or-completion ()
    (interactive)
    (let ((yas-fallback-behavior nil))
      (unless (yas-expand)
	(call-interactively #'company-complete-common))))

  (use-package yasnippet
    :diminish yas-minor-mode
    :init
    (setq yas-snippet-revival nil)
    :hook
    (prog-mode . yas-minor-mode) ;; don't need them in org
    (company-mode . (lambda() 
		      (substitute-key-definition 'company-complete-common
						 'company-yasnippet-or-completion
						 company-active-map))))

  (use-package yasnippet-snippets)
#+end_src
** LSP Mode and LSP UI
#+begin_src emacs-lisp
(use-package lsp-mode
  :commands (lsp lsp-deferred)

  :hook
  (go-mode . lsp)
  (rjsx-mode . lsp)
  (css-mode . lsp)
  (html-mode . lsp)
  (help-mode . visual-line-mode) ;; visual line mode for docs

  :init
  (setq lsp-keymap-prefix "C-c l")

  :config
  (setq gc-cons-threshold 100000000)
  (setq read-process-output-max (* 1024 1024)) ;; 1mb
  (setq lsp-log-io nil) ;; just in case
  (setq lsp-completion-provider :capf) ;; All you need
  (setq create-lockfiles nil) ;; disable lockfiles because they annoy some LSP

  (setq lsp-headerline-breadcrumb-enable nil) ;; disable breadcrumb
  (setq lsp-enable-symbol-highlighting nil) ;; disable symbol highlight
  (setq lsp-enable-links nil) ;; disable links

  (setq lsp-enable-indentation nil) ;; Don't let LSP mess with indentation
  (setq lsp-enable-on-type-formatting nil) ;; Don't ever format unless we say so

  (lsp-enable-which-key-integration t)

  ;; Make help buffers nicer
  (add-to-list 'display-buffer-alist
               '((lambda (buffer _) (with-current-buffer buffer
                                      (seq-some (lambda (mode)
                                                  (derived-mode-p mode))
                                                '(help-mode))))
                 (display-buffer-reuse-window display-buffer-below-selected)
                 (reusable-frames . visible)
                 (window-height . 0.30)))
  )

(use-package lsp-ui
  :hook (lsp-mode . lsp-ui-mode)
  :config
  (setq lsp-ui-doc-position 'at-point
        lsp-ui-doc-enable nil
        lsp-ui-sideline-enable nil))
#+end_src
** Major mode config
*** Python
#+begin_src emacs-lisp
(use-package lsp-pyright
  :hook (python-mode . (lambda ()
                         (require 'lsp-pyright)
                         (lsp))))
#+end_src
*** Go
#+begin_src emacs-lisp
(use-package go-mode)
#+end_src
*** RJSX
#+begin_src emacs-lisp
(use-package rjsx-mode
  :mode
  ("\\.[jt]sx?\\'")
  :hook
  (rjsx-mode . (lambda ()
                 (js2-mode-hide-warnings-and-errors)
		 (setq-local tab-width 2)))
  :config
  (setq js-indent-level 2))
#+end_src
*** Org
**** General Settings
#+begin_src emacs-lisp
(use-package org
  :init
  (setq org-adapt-indentation nil
        org-indent-indentation-per-level 1        
        org-src-tab-acts-natively t ;; Better tabs in source blocks
        org-src-preserve-indentation t ;; Don't auto tab in source block
        org-catch-invisible-edits 'smart ;; Possibly better editing with folds
        org-special-ctrl-a/e t
	org-table-copy-increment nil ;; don't increment table on S-RET
        )
  :hook
  (org-mode . visual-line-mode)
  :config
  (setq org-directory "~/git_repos/emacs-org-mode"
        org-agenda-files '("~/git_repos/emacs-org-mode/School.org" "~/git_repos/emacs-org-mode/Work.org"))
  ;; Following two lines are a workaround so that special a/e works
  ;; in visual line mode.
  (define-key org-mode-map "\C-a" 'org-beginning-of-line)
  (define-key org-mode-map "\C-e" 'org-end-of-line)
  )
#+end_src
**** Babel settings
#+begin_src emacs-lisp
;; BABEL LANGUAGES
(org-babel-do-load-languages
 'org-babel-load-languages
 '((emacs-lisp . t)
   (python . t)))
(push '("conf-unix" . conf-unix) org-src-lang-modes)

;; Automatically tangle our Emacs.org config file when we save it
(defun jackson/org-babel-tangle-config ()
  (when (string-equal (buffer-file-name)
                      (expand-file-name "~/.config/nixpkgs/configs/emacs/Config.org"))
    ;; Dynamic scoping to the rescue
    (let ((org-confirm-babel-evaluate nil))
      (org-babel-tangle))))

(add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'jackson/org-babel-tangle-config)))
#+end_src
* Other package configuration
